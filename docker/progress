function eta(p, s) {
  if (p <= 0 || p >= 1)
    return 0;
  else
    return hms(s/p - s);
}

function hms(s,    h, m) {
  s = int(s)
  h = int(s/3600); s = s - (h*3600);
  m = int(s/60); s = s - (m*60);
       if (h > 0) return h "h" m "m" s "s";
  else if (m > 0) return m "m" s "s";
  else            return s "s";
}

$1 ~ /^[0-9]+/ && $2 ~ /^[0-9]+/ {
  t = strftime("%T%z"); # timestamp
  s = int($1);          # seconds elapsed
  b = int($2);          # bytes processed
  p = b / total;        # percentage
  if (p > 1) p = 1      # set a maximum percentage
  e = eta(p, s)         # ETA
  printf("%s %0.2f%% complete, %s elapsed", t, (p*100), hms(s));
  if (e != 0)
    printf(", %s remaining", e)
  print("") # for newline
  fflush(stdout);
}
